use ProtossProbe as knowledge.
use "../General/General" as knowledge.
use "../General/GeneralEvents" as module.

module ProtossProbeEvent {
	
	% Find all friendly Units
	forall bel(friendly(Type,Id,Conditions)), not(percept(friendly(Type,Id,Conditions))) do delete(friendly(Type,Id,Conditions)).
	forall percept(friendly(Type,Id,Conditions)), not(bel(friendly(Type,Id,Conditions))) do insert(friendly(Type,Id,Conditions)).	
	
	% Received builder task from the Manager
	if (_).sent:(builder)
		then insert(builder).
		
	% Received gasCollector task from the Manager
	if (_).sent:(gasCollector)
		then insert(gasCollector).
		
	% Run the general module
	if true
		then GeneralEvents.	
		
	% The handling of the mineralfields	percepts (update amounts).
	% Amount is always >1, as mineralFields dissapear from the game when empty.
	forall bel(mineralField(Id,Amount)), not(percept(mineralField(Id,Amount,_,_,_))) do delete(mineralField(Id,Amount)).
	forall percept(mineralField(Id,Amount,_,_,_)), not(bel(mineralField(Id,Amount))) do insert(mineralField(Id,Amount)).
	
	% Become aware of all the vespeneGeysers
	forall percept(vespeneGeyser(Id,Amount,_,X,Y)), not(bel(vespeneGeyser(Id,Amount,X,Y))) 
		do insert(vespeneGeyser(Id,Amount,X,Y)).
	
	% The handling of the workerActivity percept
	if percept(workerActivity(NewActivity)), bel(workerActivity(OldActivity), NewActivity \= OldActivity) 
		then delete(workerActivity(OldActivity)) + insert(workerActivity(NewActivity)).
	
	% If we need gas set target to build an Assimilator
	if not(goal(unitAmount("Protoss Assimilator", 1))), bel(builder, vespeneGeyser(_,_,_,_)) 
		then adopt(unitAmount("Protoss Assimilator", 1)).
		
	% If we need Pylon, set target to build an Assimilator
	if not(goal(unitAmount("Protoss Pylon", 1))), bel(builder), bel(resources(_,_,Y,X), Y + 4 >= X)
		then adopt(unitAmount("Protoss Pylon", 1)).
		
	% If we need gas set target to build an Assimilator
	if bel(friendly("Protoss Assimilator",ID,_)), bel(gasCollector)
		then adopt(workerActivity(gatheringGas)).
	
	% Check the gathered goals
	if goal(gathered(Id)), not(bel(mineralField(Id,_))) then insert(gathered(Id)).
	
}